apply plugin: 'com.android.model.native'

model {
    repositories {

        libs(PrebuiltLibraries) { libs ->
            LIBS_DEF.each { entry ->
                libs.create(entry[0]) {
                    if (entry[2] != "") {
                        headers.srcDir entry[2]
                    }
                    entry[1].each { binEntry ->
                        binaries.withType(SharedLibraryBinary) {
                            sharedLibraryFile = file("${PROJECT_PATH_BASE}/ext-output/${targetPlatform.getName()}/${binEntry}")
                        }
                    }
                }
            }
        }
    }

    android {
        compileSdkVersion VCMI_COMPILE_SDK
        buildToolsVersion "25.0.0"

        defaultConfig {
            minSdkVersion.apiLevel 16
            targetSdkVersion.apiLevel 24
        }
    }

    android.ndk {
        moduleName "vcmi-server"
        debuggable true
        platformVersion VCMI_PLATFORM
        toolchain "clang"
        stl VCMI_STL_VERSION
        cppFlags.addAll("-std=c++11",
                "-fcxx-exceptions",
                "-frtti",
                "-g",
                "-I" + file("${VCMI_PATH_VCMI}/include").absolutePath,
                "-I" + file("${VCMI_PATH_VCMI}/server").absolutePath,
                "-I" + file("${VCMI_PATH_VCMI}/lib").absolutePath,
                "-I" + file("${VCMI_PATH_VCMI}").absolutePath,
                "-I" + file("${VCMI_INCL_SDL}").absolutePath,
                "-I" + file("${VCMI_INCL_BOOST}").absolutePath)
        ldLibs.addAll("z", "log", "atomic")
        abiFilters.addAll(VCMI_ABIS)
    }

    android.sources {
        main {
            jni {
                source {
                    srcDir "${VCMI_PATH_VCMI}/server"
                    srcDir "src/main/jni"
                }
                dependencies {
                    library "prebuiltSDL" linkage "shared"
                    project ":minizip"
                    project ":vcmi-lib"
                    project ":boost-datetime"
                    project ":boost-locale"
                    project ":boost-system"
                    project ":boost-filesystem"
                    project ":boost-smartptr"
                    project ":boost-program-options"
                    project ":boost-thread"
                }
            }
        }
    }
}